trace { break }

syntax
  { 'Term = ['Tag [
       ['Lam ['Bind 'Term ['Rec 'Term]]]
       ['App ['Rec 'Term] ['Rec 'Term]]
    ]]
  ; 'Stack = ['NilOrCons ['Rec 'Term] ['Rec 'Stack]]
  ; 'Entry = ['Tag [
       ['Argument ['Rec 'Term]]
       ['Variable ['Rec 'Term]]
     ]]
  }

krivine : ?'Term. ?'Stack. !'Term.
unwind : ?'Term. ?'Stack. !'Term.

unwind@p = p?tm. p?stk. case stk
  { [] -> case tm
    { ['Lam \x.b] ->
       -- stack is empty: evaluate under a binder with a placeholder variable
       \x. krivine { x -> ['Variable x] }.
           krivine@q. q!b. q![]. q?res. p!['Lam \x.res].
    ; _ -> p!tm.
    }
  ; [a | as] -> case tm
    { ['Lam \x.b] ->
      -- stack is not empty: add an environment entry for x
      \x. krivine { x -> ['Argument a] }.
      krivine@q. q!b. q!as. q?res.
      case res { {x*}rr -> p!rr.
               ; _ -> # "This should not happen! %r should not occur in %i" x res
               }
    ; _ -> unwind@q. q!['App tm a]. q!as. q?res. p!res.
    }
  }

krivine@p = p?tm. p?stk.
  lookup tm { rec -> case rec
    -- if the variable maps to an argument, we have some more
    -- evaluating to do. Otherwise we can just unwind the stack
    -- on top of the placeholder variable.
    { ['Argument val] -> krivine@q. q!val. q!stk. q?res. p!res.
    ; ['Variable var] -> unwind@q. q!var. q!stk. q?res. p!res.
    }}
  else case tm
  { ['App f t] -> ?vt. ( krivine@q. q!t. q![]. q?res. res ~ vt
                       | krivine@q. q!f. q![vt|stk]. q?res. p!res.
                       )
  ; _ -> unwind@q. q!tm. q!stk. q?res. p!res.
  }

exec krivine@p.
  p!['App ['Lam \f. ['Lam \x. ['App f ['App f x]]]]
          ['Lam \g. ['Lam \y. ['App g ['App g y]]]]].
  p![]. p?v. PRINTF "%i" v .
